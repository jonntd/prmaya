/*
SOURCE
https://github.com/parzival-roethlein/prmaya

DESCRIPTION
in prDeformPaint.py
*/

global string $prDP_currentCtx;
// from Ui
global int $prDP_operation;
global string $prDP_driver;
// for every stroke
global string $prDP_driven = "";
global float $prDP_deltaMultiplier = 1.0;


// initialize paint tool
global proc prDeformPaint_initialize()
{
	global string $prDP_currentCtx;
	ScriptPaintTool;
	artUserPaintCtx -e -tsc "prDeformPaint" `currentCtx`;
}


// This procedure should be set as the "Tool Setup Cmd" in the 
// Setup tab of the Maya Artisan Script Paint tool's tool settings
// window. The tool context is supplied as an argument.
global proc prDeformPaint(string $context)
{
    // initialize all the other commands in this scriptable paint tool context.
    artUserPaintCtx -e
        -initializeCmd "prDP_initializeCmd"
        -finalizeCmd "prDP_finalizeCmd"
        -setValueCommand "prDP_setValueCommand"
        -gvc ""
        -gsc ""
        -cc ""
        -tcc ""
        -gac ""
        -duringStrokeCmd ""
        $context;
    global string $prDP_currentCtx;
    $prDP_currentCtx = $context;
}

// This is the "Initialize Cmd". This procedure is called once
// for every selected surface when an initial click is received
// on any surface. The argument is the name of the surface. This
// procedure returns a string which indicates to the scriptable
// tool how to behave for the duration of the stroke.
global proc string prDP_initializeCmd(string $name)
{
    global string $prDP_driven;
    $prDP_driven = $name;
    global float $prDP_deltaMultiplier;
    $prDP_deltaMultiplier = python("prDeformPaint.getEditBlendshapeMultiplier('"+$prDP_driven+"')");
    return "";
}

// This is the "Finalize Cmd". This procedure is called at the
// end of the stroke. It is passed the surface ID, that was
// generated by the "Initialize Cmd".
global proc prDP_finalizeCmd(int $surfaceID)
{
    global string $prDP_driven;
    $prDP_driven = "";
    global float $prDP_deltaMultiplier;
    $prDP_deltaMultiplier = 1.0;
}

// This is the "Set Value Cmd". It is called every time a value
// on the surface is changed. A surface ID, a grid index
// on the surface and the value associated with that grid index
// is passed. There can be additional arguments depending on the
// options generated by the return value of the "Initialize Cmd".
// In this case the (x,y,z) surface position for this grid point
// is also passed.
global proc prDP_setValueCommand(int $slot, int $index, float $weight)
{
    global int $prDP_operation;
    global float $prDP_deltaMultiplier;
    global string $prDP_driver;
    global string $prDP_driven;
    if($prDP_operation == 0)
        python("prDeformPaint.averageDeltas('"+$prDP_driver+"', '"+$prDP_driven+"', ["+$index+"], ["+$weight+"], "+$prDP_deltaMultiplier+")");
    else if($prDP_operation == 1)
        python("prDeformPaint.copyPosition('"+$prDP_driver+"', '"+$prDP_driven+"', ["+$index+"], ["+$weight+"], "+$prDP_deltaMultiplier+")");
}

